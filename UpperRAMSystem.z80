; ------------------------------------------------------------------------------
; UpperRAMSystem.z80
; ------------------------------------------------------------------------------
; This is the entirety of the system stuff that gets loaded to the top of RAM
; at system startup time
; ------------------------------------------------------------------------------
    ORG SystemRAMBase

SysTemplateStart:

VideoDriver:
    DS  $400
SoundDriver:
    DS  $400
StorageDriver:
    DS  $400
System:
    DS  $700
SyscallTable:
    INCLUDE "SyscallTable.z80"
    DS  $80 - ($ - SyscallTable)            ; Pad out the table to account for unused calls

SyscallTarget:  DS 1
ScratchPad:     DS $6B
HardwareID4:    DS 1
HardwareID5:    DS 1
HardwareID6:    DS 1
HardwareID7:    DS 1

InterruptTable:                             ; Interrupt Mode 2 vector table
    DW NullInterruptHandler
    DW PS2Keyboard.IRQHandler
    DW Serial.IRQHandler
    DW NullInterruptHandler
    DW NullInterruptHandler
    DW NullInterruptHandler
    DW NullInterruptHandler
    DW NullInterruptHandler

    SECTION System
ColdBoot:
    ; Turn on 64K mode
    LD      A, 1
    OUT     (0), A

    ; Copy the base RAM template over
    LD      BC, $0100
    LD      HL, $8000
    LD      DE, $0000
    LDIR
    
    ; Continue with warm boot
WarmBoot:
    CALL    BufferedInput.Initialize
    CALL    PS2Keyboard.Initialize
    CALL    Serial.Initialize

    ; Clear screen
    LD      A, Syscall.PrintChar
    LD      (SyscallTarget), A
    LD      A, ASCII.FF
    RST     $10

    ; Print welcome messages
    LD      A, Syscall.PrintString
    LD      (SyscallTarget), A
    LD      HL, StartupMessage
    RST     $10

    ; Attempt to boot from storage driver
    ; If not, fall back to the monitor
    JP      Monitor.Start

SyscallExec:
    PUSH    AF                              ; Preserve AF
    LD      A, (SyscallTarget)              ; Get the syscall number
    LD      (SyscallJump + 1), A            ; Write the jump address low byte
    POP     AF                              ; Restore AF
SyscallJump:
    JP      $FF00                           ; This jump address is overwritten by the preceding code

NullInterruptHandler:
    RETI

StartupMessage:
    DB ASCII.FF
    DB "               Interis 64"
    DB ASCII.CR, ASCII.LF
    DB "            System ROM v1.0"
    DB ASCII.CR, ASCII.LF
    DB ASCII.CR, ASCII.LF
    DB "    (C) 2023 M. Martin - GNU GPL 3.0"
    DB ASCII.CR, ASCII.LF
    DB ASCII.CR, ASCII.LF
    DB "      https://github.com/Interis64"
    DB ASCII.CR, ASCII.LF
    DB $00


    INCLUDE "Monitor.z80"
    INCLUDE "BufferedInput.z80"
    INCLUDE "NVRAM.z80"
    INCLUDE "ASCII.z80"
    INCLUDE "Serial.z80"
    INCLUDE "Utils.z80"
    INCLUDE "PS2Keyboard.z80"
    ENDS

; ------------------------------------------------------------------------------
; BaseSystem.z80
; ------------------------------------------------------------------------------
; Interis startup and system call code. This contains the cold boot code and
; code for the various system calls available via RST $10.
; ------------------------------------------------------------------------------
; Interis 64 Z80 Computer
; https://github.com/Interis64
; 
; Copyright 2023 Matthew Martin
; Licensed under the GNU GPL 3.0. See the LICENSE file for details.
; ------------------------------------------------------------------------------

ColdBoot:
    ; Turn on 64K mode
    LD      A, $01
    OUT     (0), A

    ; Copy the base RAM template over
    LD      BC, $0100
    LD      HL, $8000
    LD      DE, $0000
    LDIR
    
    ; Continue with warm boot
WarmBoot:
    CALL    PS2Keyboard.Initialize
    CALL    Serial.Initialize
    CALL    BufferedInput.Initialize

    ; Clear screen
    LD      A, Syscall.PrintChar
    LD      (SyscallTarget), A
    LD      A, $0C
    RST     $10

    ; Print welcome messages
    LD      A, Syscall.PrintString
    LD      (SyscallTarget), A
    LD      HL, StartupMessage
    RST     $10

    ; Attempt to boot from storage driver
    ; If not, fall back to the monitor
    JP      Monitor.Start

SyscallExec:
    POP     HL                              ; This was pushed by the RST10 call
    PUSH    AF                              ; Preserve AF
    LD      A, (SyscallTarget)              ; Get the syscall number
    LD      (SyscallJump + 1), A            ; Write the jump address low byte
    POP     AF                              ; Restore AF
SyscallJump:
    JP      $FF00                           ; This jump address is overwritten by the preceding code

NullInterruptHandler:
    EI
    RETI

StartupMessage:
    DB $0C                                  ; Form Feed
    DB "               Interis 64\r\n"
    DB "            System ROM v1.0\r\n"
    DB "\r\n"
    DB "    (C) 2023 M. Martin - GNU GPL 3.0\r\n"
    DB "\r\n"
    DB "      https://github.com/Interis64\r\n"
    DB $00

    INCLUDE "BufferedInput.z80"
    INCLUDE "PS2Keyboard.z80"
    INCLUDE "Serial.z80"
    INCLUDE "NVRAM.z80"
    INCLUDE "Utils.z80"
    INCLUDE "Monitor.z80"
; ------------------------------------------------------------------------------
; Monitor.z80
; ------------------------------------------------------------------------------
; System monitor/debugger
; ------------------------------------------------------------------------------
; Interis 64 Z80 Computer
; https://github.com/Interis64
; 
; Copyright 2023 Matthew Martin
; Licensed under the GNU GPL 3.0. See the LICENSE file for details.
; ------------------------------------------------------------------------------
; Commands
; M addr        - Start interactive memory editor at address 'addr'
; D addr size   - Dump 'size' bytes of raw memory over serial port starting with address 'addr'
; X addr        - Jump to memory address hhll and execute code
; Q             - Quit the monitor (resume if we broke in via NMI, reset otherwise)
; ------------------------------------------------------------------------------

Monitor.CommandBufferMax:       EQU 16                                  ; Max of 16 chars for commands
Monitor.CommandBufferLength:    DS  1                                   ; Memory location for the current size of the command buffer
Monitor.CommandBuffer:          DS  Monitor.CommandBufferMax            ; Memory location for the command buffer (16 bytes)
Monitor.DataBufferMax:          EQU 23                                  ; Max of 23 chars for data (8 hex bytes with spaces between)
Monitor.DataBufferLength:       DS  1                                   ; Memory location for the current size of the data buffer
Monitor.DataStartAddress:       DS  2                                   ; Memory location for the Memory Editor start address
Monitor.DataBuffer:             DS  Monitor.DataBufferMax               ; Memory location for the data buffer (23 bytes)

; If we got here from a non-maskable interrupt, then we'll swap out the registers
; so we don't clobber the running program. When the Monitor exits, we
; swap them back and give control back.
Monitor.NMIHandler:
    EX      AF, AF'
    EXX

    ; Call instead of jump, so we put this return address on the stack
    CALL    Monitor.ControlLoop
    
    ; When the user quits the monitor, we'll pick up here
    EXX
    EX      AF, AF'
    RETN

; If we got here from an external call, then we'll swap out the registers
; so we don't clobber the running program. When the Monitor exits, we
; swap them back and give control back.
Monitor.DebugHandler:
    EX      AF, AF'
    EXX

    ; Call instead of jump, so we put this return address on the stack
    CALL    Monitor.ControlLoop
    
    ; When the user quits the monitor, we'll pick up here
    EXX
    EX      AF, AF'
    RET

Monitor.Start:
    LD      HL, $0000                       ; Push $0000 to the stack so we do a cold reset on a quit command
    PUSH    HL

Monitor.ControlLoop:
    CALL    Monitor.AcceptInput
    CALL    Monitor.ProcessInput
    JP      Monitor.ControlLoop

; -------------------------------------
; Accept Input - Reads up to 16 bytes of ASCII from the user
; -------------------------------------
Monitor.AcceptInput:
    LD      B, Monitor.CommandBufferMax
    XOR     A
    LD      HL, Monitor.CommandBuffer               ; Point HL at the command buffer
Monitor.ClearInputLoop:
    LD      (HL), A
    INC     HL
    DJNZ    Monitor.ClearInputLoop

    LD      HL, Monitor.CommandPrompt               ; Print our prompt
    CALL    Utils.PrintString
    XOR     A                                       ; Zero out the length
    LD      (Monitor.CommandBufferLength), A        
    LD      HL, Monitor.CommandBuffer               ; Point HL at the command buffer
Monitor.AcceptInputLoop:
    SYSCALL SC_ReadChar

    CP      '\n'                                    ; Ignore LF character
    JP      Z, Monitor.AcceptInputLoop

    CP      '\r'                                    ; CR completes input
    RET     Z

    CP      $08                                     ; Handle Backspace
    JP      Z, Monitor.AcceptBackspace

    CP      $1B
    JP      Z, Monitor.AcceptInput                  ; Esc key aborts the current line and prompts again

    LD      B, A
    LD      A, (Monitor.CommandBufferLength)        ; Check if we're at the max input size
    CP      Monitor.CommandBufferMax
    JP      Z, Monitor.AcceptInputLoop              ; If we're at max size, do nothing
    LD      A, SC_PrintChar
    LD      (SyscallTarget), A
    LD      A, B
    RST     $10                                     ; Echo back to the screen
    LD      (HL), A                                 ; Stuff in the command buffer
    INC     HL                                      ; Move to the next location
    LD      A, (Monitor.CommandBufferLength)        ; Increment our length counter
    INC     A
    LD      (Monitor.CommandBufferLength), A        
    JP      Monitor.AcceptInputLoop                 ; Wait for the next input

Monitor.AcceptBackspace:
    LD      A, (Monitor.CommandBufferLength)        ; Load our length counter
    CP      $00                                     ; If we're at zero length, can't backspace
    JP      Z, Monitor.AcceptInputLoop              ; So just go back to our input loop
    LD      A, SC_PrintChar
    LD      (SyscallTarget), A
    LD      A, $08
    RST     $10                                     ; Output the backspace char
    DEC     HL                                      ; Go back a spot
    LD      A, (Monitor.CommandBufferLength)        ; Decrement our length counter
    DEC     A
    LD      (Monitor.CommandBufferLength), A
    JP      Monitor.AcceptInputLoop

; -------------------------------------
; Process Input - Decides what to do based on the user's input
; -------------------------------------
Monitor.ProcessInput:
    LD      A, (Monitor.CommandBufferLength)
    CP      0
    RET     Z
    LD      HL, Monitor.CommandBuffer
    LD      A, (HL)
    CP      'H'
    JP      Z, Monitor.ProcessHelpCommand
    CP      'h'
    JP      Z, Monitor.ProcessHelpCommand
    CP      'M'
    JP      Z, Monitor.ProcessMemoryEditorCommand
    CP      'm'
    JP      Z, Monitor.ProcessMemoryEditorCommand
    CP      'X'
    JP      Z, Monitor.ProcessExecuteCommand
    CP      'x'
    JP      Z, Monitor.ProcessExecuteCommand
    CP      'D'
    JP      Z, Monitor.ProcessDumpCommand
    CP      'd'
    JP      Z, Monitor.ProcessDumpCommand
    CP      'I'
    JP      Z, Monitor.ProcessInputCommand
    CP      'i'
    JP      Z, Monitor.ProcessInputCommand
    CP      'O'
    JP      Z, Monitor.ProcessOutputCommand
    CP      'o'
    JP      Z, Monitor.ProcessOutputCommand
    CP      'Q'
    JP      Z, Monitor.ProcessQuitCommand
    CP      'q'
    JP      Z, Monitor.ProcessQuitCommand

    ; Unknown, so complain to the user and ask again
Monitor.CommandError:
    LD      HL, Monitor.UnknownCommandString
    JP      Utils.PrintString

; -------------------------------------
; Memory Editor Command [M addr]
; -------------------------------------
Monitor.ProcessHelpCommand:
    LD      HL, Monitor.HelpText
    JP      Utils.PrintString

; -------------------------------------
; Memory Editor Command [M addr]
; -------------------------------------
Monitor.ProcessMemoryEditorCommand:
    LD      A, (Monitor.CommandBufferLength) ; Check command length
    CP      6                               ; 6 bytes
    JP      NZ, Monitor.CommandError
    LD      HL, Monitor.CommandBuffer+2
    CALL    Monitor.ReadHexWord
    EX      DE, HL
    LD      (Monitor.DataStartAddress), HL

    LD      A, '\n'
    SYSCALL SC_PrintChar
    JP      Monitor.ME_ControlLoop

Monitor.MemoryEditorCommandError:
    LD      HL, Monitor.MemoryEditorCommandErrorString
    JP      Utils.PrintString

Monitor.MemoryEditorCommandErrorString:
    DB      " ?? Syntax Error, expected \"M addr\"", $00


Monitor.ME_ControlLoop:
    CALL    Monitor.ME_PrintMemory
    CALL    Monitor.ME_AcceptInput
    CALL    Monitor.ME_ProcessInput
    JP      Monitor.ME_ControlLoop

; Prints out 8 bytes of memory from the current address pointer as hex
Monitor.ME_PrintMemory:
    LD      A, '\r'
    SYSCALL SC_PrintChar
    LD      A, '['
    SYSCALL SC_PrintChar
    LD      HL, (Monitor.DataStartAddress)  ; Get the address pointer
    EX      DE, HL                          ; Swap to DE
    CALL    Monitor.PrintHexWord            ; Print address pointer
    LD      A, ']'
    SYSCALL SC_PrintChar

    LD      B, 8
Monitor.ME_PrintMemoryLoop:
    LD      A, ' '
    SYSCALL SC_PrintChar
    LD      A, (HL)
    CALL    Monitor.PrintHexByte
    INC     HL
    DJNZ    Monitor.ME_PrintMemoryLoop
    RET

Monitor.ME_AcceptInput:
    LD      A, ' '                          ; Clear out the input buffer with blanks
    LD      HL, Monitor.DataBuffer
    LD      B, Monitor.DataBufferMax
Monitor.ME_ClearInputBufferLoop:
    LD      (HL), A
    INC     HL
    DJNZ    Monitor.ME_ClearInputBufferLoop
    XOR     A                               ; Set the buffer length to zero
    LD      (Monitor.DataBufferLength), A

    LD      HL, Monitor.DataPrompt          ; Print our prompt
    CALL    Utils.PrintString
    LD      HL, Monitor.DataBuffer          ; Point HL at the data buffer
Monitor.ME_AcceptInputLoop:
    CALL    BufferedInput.Read
    CP      '\r'                            ; CR completes input
    RET     Z                               ; So return out of the input loop
    CP      $08                             ; Ignore LF character
    JP      Z, Monitor.ME_AcceptInputLoop
    CP      $08                             ; Handle Backspace
    JP      Z, Monitor.ME_AcceptBackspace
    CP      $1B
    JP      Z, Monitor.ME_AcceptEscape      ; Esc key handler

    LD      B, A                            ; Temporary store of input char
    LD      A, (Monitor.DataBufferLength)   ; Check if we're at the max input size
    CP      Monitor.DataBufferMax
    JP      Z, Monitor.ME_AcceptInputLoop   ; If we're at max size, do nothing
    LD      A, SC_PrintChar
    LD      (SyscallTarget), A
    LD      A, B                            ; Restore input char
    RST     $10                             ; Echo back to the screen
    LD      (HL), A                         ; Stuff into the data buffer
    INC     HL                              ; Move to the next location
    LD      A, (Monitor.DataBufferLength)   ; Increment our length counter
    INC     A
    LD      (Monitor.DataBufferLength), A        
    JP      Monitor.ME_AcceptInputLoop      ; Wait for the next input

Monitor.ME_AcceptEscape:
    LD      A, (Monitor.DataBufferLength)   ; Load our length counter
    CP      $00                             ; If we've entered text, clear it and accept input again
    JP      NZ, Monitor.ME_AcceptInput              
                                            ; But if it's a blank line, we exit the memory editor
    POP     HL                              ; Pop off the extra stack pointer
    RET                                     ; This should return back to the main monitor loop

Monitor.ME_AcceptBackspace:
    LD      A, (Monitor.DataBufferLength)   ; Load our length counter
    CP      $00                             ; If we're at zero length, can't backspace
    JP      Z, Monitor.ME_AcceptInputLoop   ; So just go back to our input loop
    LD      A, SC_PrintChar
    LD      (SyscallTarget), A
    LD      A, $08                     ;
    RST     $10                             ; Output the backspace char
    DEC     HL                              ; Go back a spot
    LD      A, (Monitor.DataBufferLength)   ; Decrement our length counter
    DEC     A
    LD      (Monitor.DataBufferLength), A
    JP      Monitor.ME_AcceptInputLoop

; -------------------------------------
; ME Process Input - Parses/writes any new data bytes and increments the address pointer
; -------------------------------------
Monitor.ME_ProcessInput:
    LD      A, (Monitor.DataBufferLength)   ; If the user entered nothing, then go to the next memory block
    CP      0
    JP      Z, Monitor.ME_IncrementAddress
    LD      HL, Monitor.DataBuffer
    CALL    Monitor.ME_CheckDataByte
    CALL    Monitor.ME_CheckDataByte
    CALL    Monitor.ME_CheckDataByte
    CALL    Monitor.ME_CheckDataByte
    CALL    Monitor.ME_CheckDataByte
    CALL    Monitor.ME_CheckDataByte
    CALL    Monitor.ME_CheckDataByte
    CALL    Monitor.ME_CheckDataByte

    LD      HL, Monitor.DataBuffer
    LD      DE, (Monitor.DataStartAddress)
    CALL    Monitor.ME_UpdateDataByte
    CALL    Monitor.ME_UpdateDataByte
    CALL    Monitor.ME_UpdateDataByte
    CALL    Monitor.ME_UpdateDataByte
    CALL    Monitor.ME_UpdateDataByte
    CALL    Monitor.ME_UpdateDataByte
    CALL    Monitor.ME_UpdateDataByte
    CALL    Monitor.ME_UpdateDataByte

    RET

Monitor.ME_IncrementAddress:
    LD      HL, (Monitor.DataStartAddress)
    INC     HL
    INC     HL
    INC     HL
    INC     HL
    INC     HL
    INC     HL
    INC     HL
    INC     HL
    LD      (Monitor.DataStartAddress), HL
    RET

Monitor.ME_CheckDataByte:
    LD      A, (HL)
    CP      ' '
    JP      Z, Monitor.ME_CheckDataByteSkip
    CALL    Monitor.TestHexDigitSpace
    JP      PO, Monitor.ME_DataError
    INC     HL
    LD      A, (HL)
    CALL    Monitor.TestHexDigitSpace
    JP      PO, Monitor.ME_DataError
    INC     HL
    INC     HL
    RET
Monitor.ME_CheckDataByteSkip:
    INC     HL
    INC     HL
    INC     HL
    RET

Monitor.ME_UpdateDataByte:
    LD      A, (HL)                         ; If it's a space, we can skip this byte
    CP      ' '
    JP      Z, Monitor.ME_UpdateDataByteSkip
    CALL    Monitor.ReadHexByte
    LD      (DE), A                         ; Store the new value
    INC     DE
    INC     HL                              ; Skip the space after the value
    RET
Monitor.ME_UpdateDataByteSkip:
    INC     DE
    INC     HL
    INC     HL
    INC     HL
    RET

Monitor.ME_DataError:
    LD      HL, Monitor.ErrorString
    CALL    Utils.PrintString
    POP     HL                              ; Pop off an extra stack frame
    RET

; -------------------------------------
; Execute Command [X addr]
; -------------------------------------
Monitor.ProcessExecuteCommand:
    LD      A, (Monitor.CommandBufferLength)        ; Check command length
    CP      6                               ; 6 bytes
    JP      NZ, Monitor.ExecuteCommandError
    LD      HL, Monitor.CommandBuffer+2
    CALL    Monitor.ReadHexWord
    PUSH    DE                              ; Push onto the stack and we'll abuse RET to jump there
    RET                                     ; This is going to jump to the address we just pushed from DE

Monitor.ExecuteCommandError:
    LD      HL, Monitor.ExecuteCommandErrorString
    JP      Utils.PrintString

Monitor.ExecuteCommandErrorString:
    DB      " ?? Syntax Error, expected \"X addr\"", $00

; Note that we don't really expect to come back from where we jumped,
; but a RET from there will send us back into the monitor loop

; -------------------------------------
; Dump Command [D addr size]
; -------------------------------------
Monitor.ProcessDumpCommand:
    LD      A, (Monitor.CommandBufferLength)        ; Check command length
    CP      11                              ; 11 bytes
    JP      NZ, Monitor.DumpCommandError
    LD      HL, Monitor.CommandBuffer+1
    LD      A, (HL)
    CP      $20                             ; Next char needs to be ASCII space
    JP      Z, Monitor.DumpCommandError
    INC     HL
    CALL    Monitor.ReadHexWord             ; This is the starting address
    PUSH    DE
    INC     HL                              ; Skip the space
    CALL    Monitor.ReadHexWord             ; This is the byte length
    LD      B, D                            ; Copy to our counter
    LD      C, E
    POP     HL                              ; Get the address into HL

Monitor.DumpLoop:
    LD      A, SC_PrintChar
    LD      (SyscallTarget), A
    LD      A, (HL)
    RST     $10
    INC     HL
    DEC     BC
    LD      A, B
    OR      C
    JP      NZ, Monitor.DumpLoop


    RET

Monitor.DumpCommandError:
    LD      HL, Monitor.DumpCommandErrorString
    JP      Utils.PrintString

Monitor.DumpCommandErrorString:
    DB      " ?? Syntax Error, expected \"D addr size\"", $00

; -------------------------------------
; Quit command [Q]
; -------------------------------------
Monitor.ProcessQuitCommand:
    LD      A, (Monitor.CommandBufferLength) ; Check command length
    CP      1                               ; 1 byte
    JP      NZ, Monitor.QuitCommandError
    CALL    Utils.PrintCRLF
    POP     HL                              ; Pop off our monitor return address
    RET                                     ; Return to the previous address (outside of Monitor)

Monitor.QuitCommandError:
    LD      HL, Monitor.QuitCommandErrorString
    JP      Utils.PrintString

Monitor.QuitCommandErrorString:
    DB      " ?? Syntax Error, expected \"Q\"", $00


; -------------------------------------
; I/O Out command [O]
; - Syntax: O XX YY
; - Where XX is the IO Adddress
; - and YY is the byte to output
; -------------------------------------
Monitor.ProcessOutputCommand:
    LD      A, (Monitor.CommandBufferLength)        ; Check command length
    CP      7                                       ; 7 bytes
    JP      NZ, Monitor.OutputCommandError
    LD      HL, Monitor.CommandBuffer+2
    CALL    Monitor.ReadHexByte
    LD      C, A
    LD      HL, Monitor.CommandBuffer+6
    OUT     (C), A
    CALL    Utils.PrintCRLF
    RET                                             ; Return to the previous address (outside of Monitor)

Monitor.OutputCommandError:
    LD      HL, Monitor.OutputCommandErrorString
    JP      Utils.PrintString

Monitor.OutputCommandErrorString:
    DB      " ?? Syntax Error, expected \"O port data\"", $00

; -------------------------------------
; I/O In command [I]
; - Syntax: I XX
; - Where XX is the IO Adddress
; -------------------------------------
Monitor.ProcessInputCommand:
    LD      A, (Monitor.CommandBufferLength)        ; Check command length
    CP      4                                       ; 4 bytes
    JP      NZ, Monitor.CommandError
    LD      HL, Monitor.CommandBuffer+2
    CALL    Monitor.ReadHexByte
    LD      C, A
    IN      A, (C)
    CALL    Monitor.PrintHexByte
    CALL    Utils.PrintCRLF
    RET                                             ; Return to the previous address (outside of Monitor)

Monitor.InputCommandError:
    LD      HL, Monitor.InputCommandErrorString
    JP      Utils.PrintString

Monitor.InputCommandErrorString:
    DB      " ?? Syntax Error, expected \"I port\"", $00

; -------------------------------------
; Data Read Command [R]
; - Syntax: R XXXX
; - Where XXXX is the address where to write the data received
; - This address is added to the offset present in the hex records
; -------------------------------------
Monitor.ProcessReadCommand:
    LD      A, (Monitor.CommandBufferLength) ; Get the entered command length
    CP      6                               ; Check if it's 6 bytes
    JP      NZ, Monitor.ReadCommandError    ; Not 6 bytes, so throw an error
    LD      HL, Monitor.CommandBuffer + 2   ; Advance to the command argument
    CALL    Monitor.ReadHexWord             ; Stores the argument in DE
    JP      Monitor.IHX.ParseRecord         ; Go to the parsing routine

Monitor.ReadCommandError:
    LD      HL, Monitor.ReadCommandErrorString
    JP      Utils.PrintString

Monitor.ReadCommandErrorString:
    DB      " ?? Syntax Error, expected \"R addr\"", $00

; ----------------------------
; Intel HEX Reader Routines
; ----------------------------
Monitor.IHX.ParseRecord:
    SYSCALL SC_ReadChar                ; Get next char from buffer
    CP      ':'                             ; Check if it's a colon
    JP      NZ, Monitor.IHX.ParseRecord     ; If not, get the next char
    CALL    Monitor.IHX.ParseByte           ; Get the record length
    CP      $00                             ; Check if this is a zero-length record
    RET     Z                               ; If so, assume we're done (typical CP/M convention)
    LD      B, A                            ; Store into B
    CALL    Monitor.IHX.ParseByte           ; Get the destination address high byte
    LD      H, A                            ; Store into H
    CALL    Monitor.IHX.ParseByte           ; Get the destination address low byte
    LD      L, A                            ; Store into L
    ADD     HL, DE                          ; Adjust the memory address
    CALL    Monitor.IHX.ParseByte           ; The the record type into A
    CP      0                               ; Is record type 0 (Data)?
    RET     NZ                              ; If not, assume EOF, unsupported, or invalid, so exit
Monitor.IHX.DataReadLoop:
    CALL    Monitor.IHX.ParseByte           ; Read the next byte
    LD      (HL), A                         ; Store at the address in HL
    INC     HL                              ; Next address
    DJNZ    Monitor.IHX.DataReadLoop        ; Go get the next byte
    JP      Monitor.IHX.ParseRecord         ; Done with this record, so get the next

; Read the current and next character, parse as hex digits, and store in A
Monitor.IHX.ParseByte:
    PUSH    DE
    SYSCALL SC_ReadChar                ; Get next char from buffer
    CALL    Monitor.IHX.ParseNibble
    ADD     A, A
    ADD     A, A
    ADD     A, A
    ADD     A, A
    LD      D, A
    SYSCALL SC_ReadChar                ; Get next char from buffer
    CALL    Monitor.IHX.ParseNibble
    OR      D
    POP     DE
    RET

Monitor.IHX.ParseNibble:
    CP      'a'
    JR      C, Monitor.IHX.ParseNibbleUC
    SUB     $20                             ; Convert Lowercase to Uppercase
Monitor.IHX.ParseNibbleUC:
    SUB     '0'
    CP      $0A
    RET     C
    SUB     'A'-'0'-10
    RET


; ------------------------------------------------------------------------------
; Hexadecimal I/O routines
; ------------------------------------------------------------------------------
; Reads a 16-bit hex value from user input into DE
; (HL should point to the start of the four-character string in the input buffer)
Monitor.ReadHexWord:
    CALL    Monitor.ReadHexByte
    LD      D, A
    CALL    Monitor.ReadHexByte
    LD      E, A
    RET

; Reads an 8-bit hex value from user input into A
; (HL should point to the start of the two-character string in the input buffer)
Monitor.ReadHexByte:
    PUSH    DE
    LD      A, (HL)
    CALL    Monitor.ReadHexNibble
    ADD     A, A
    ADD     A, A
    ADD     A, A
    ADD     A, A
    LD      D, A
    INC     HL
    LD      A, (HL)
    CALL    Monitor.ReadHexNibble
    OR      D
    INC     HL
    POP     DE
    RET

Monitor.ReadHexNibble:
    CP      'a'
    JR      C, Monitor.ReadHexNibbleUC
    SUB     $20                             ; Convert Lowercase to Uppercase
Monitor.ReadHexNibbleUC:
    SUB     '0'
    CP      $0A
    RET     C
    SUB     'A'-'0'-10
    RET

; Prints the hex string representing the word value in DE
Monitor.PrintHexWord:
    LD      A, D
    CALL    Monitor.PrintHexByte
    LD      A, E
    ; Fall through to PrintHexByte

; Prints the hex string representing the byte value in A
Monitor.PrintHexByte:
    PUSH    AF
    RRA
    RRA
    RRA
    RRA
    CALL    Monitor.PrintHexNibble
    POP     AF
    ; Fall through to PrintHexNibble

; Prints the hex character representing the low nibble of A
Monitor.PrintHexNibble:
    AND     $0F
    ADD     A, $90
    DAA
    ADC     A, $40
    DAA
    SYSCALL SC_PrintChar
    RET

; Tests if A contains a valid hex digit
; Afterwards, you can do this:
; 	jp po,not_found
Monitor.TestHexDigit:
    PUSH    HL
    LD      HL, Monitor.HexDigits
    LD      BC, 22                          ; 22 possible characters
    CPIR
    POP     HL
    RET

; Tests if A contains a valid hex digit or a space
; Afterwards, you can do this:
; 	jp po,not_found
Monitor.TestHexDigitSpace:
    PUSH    HL
    PUSH    BC
    LD      HL, Monitor.HexDigits
    LD      BC, 23                          ; 23 possible characters
    CPIR
    POP     BC
    POP     HL
    RET

Monitor.HexDigits:
    DB      "0123456789ABCDEFabcdef "
Monitor.CommandPrompt:
    DB      "\r\n] ", $00
Monitor.DataPrompt:
    DB      "\r\n     : ", $00
Monitor.ErrorString:
    DB      "\r\n ?? Unknown Error", $00
Monitor.UnknownCommandString:
    DB      "\r\n ?? Unknown Command. 'H' for help.", $00
Monitor.HelpText:
    DB      "\r\n\r\n"
    DB      "Interis 64 ROM Monitor\r\n\r\n"
    DB      "Commands:\r\n"
    DB      "  M xxxx\r\n"
    DB      "  - Start interactive memory editor\r\n"
    DB      "    at address xxxx\r\n"
    DB      "\r\n"
    DB      "  X xxxx\r\n"
    DB      "  - Start execution at address xxxx\r\n"
    DB      "\r\n"
    DB      "  R xxxx\r\n"
    DB      "  - Read Intel HEX data from input\r\n"
    DB      "  - Store starting at mem addr xxxx\r\n"
    DB      "\r\n"
    DB      $00